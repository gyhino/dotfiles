# vim:ft=zsh

# to avoid overwritten PATH via /usr/libexec/path_helper
# refs. https://github.com/Homebrew/homebrew-core/pull/32074#issuecomment-421381869
unsetopt GLOBAL_RCS

typeset -xT SUDO_PATH sudo_path
typeset -U sudo_path
sudo_path=({/usr/local,/usr,}/sbin(N-/))

typeset -gx -U path
path=( \
    ~/bin(N-/) \
    /usr/local/bin(N-/) \
    /usr/local/sbin(N-/) \
    ${path} \
    )

typeset -gx -U fpath
fpath=( \
    ~/.zsh/Completion(N-/) \
    ~/.zsh/functions/*(N-/) \
    ~/.zsh/plugins/zsh-completions(N-/) \
    /usr/local/share/zsh/site-functions(N-/) \
    ${fpath} \
    )

export XDG_CONFIG_HOME=${HOME}/.config
export XDG_CACHE_HOME=${HOME}/.cache

export LANGUAGE='en_US.UTF-8'
export LANG=${LANGUAGE}
export LC_ALL=${LANGUAGE}
export LC_CTYPE=${LANGUAGE}

export LESSCHARSET=utf-8
export JLESSCHARSET=japanese
export LESS='-qR'

export GREP_OPTIONS='--color=auto'
export GREP_COLOR='1;41;37'

export LSCOLORS=gxfxcxdxbxegedabagacad
if [[ -f "${HOME}/.dir_colors" ]]; then
    if which gdircolors > /dev/null; then
        eval `gdircolors -b ${HOME}/.dir_colors`
    elif which dircolors > /dev/null; then
        eval `dircolors -b ${HOME}/.dir_colors`
    fi
fi

export EDITOR=vim

export FIGNORE=${FIGNORE}:.svn:.git

# Oracle
export NLS_LANG=JAPANESE_JAPAN.UTF8
export NLS_DATE_FORMAT="YYYY-MM-DD HH24:MI:SS"
export SQLPATH=${HOME}/.oracle

# anyenv
if type anyenv &>/dev/null; then
    ANYENV_HOME=${HOME}/.anyenv
    ANYENV_CACHE_HOME=${XDG_CACHE_HOME}/anyenv

    # if anyenv is installed by homebrew, make the `$HOME/.anyenv` directory
    [[ -d ${ANYENV_HOME} && -d ${ANYENV_CACHE_HOME} ]] || {
        mkdir -p ${ANYENV_HOME}
        mkdir -p ${ANYENV_CACHE_HOME}
    }

    path=(${HOME}/.anyenv/bin(N-/) ${path})
    [[ -f ${ANYENV_CACHE_HOME}/anyenv.cache ]] || {
        anyenv init - zsh > ${ANYENV_CACHE_HOME}/anyenv.cache
        zcompile ${ANYENV_CACHE_HOME}/anyenv.cache
    }
    source ${ANYENV_CACHE_HOME}/anyenv.cache
fi

# GOLANG
export GOPATH=$HOME/go
export GOBIN=${GOPATH}/bin
path=(${GOBIN}(N-/) ${path})

# Rust
if [[ -f $HOME/.cargo/env ]]; then
    source $HOME/.cargo/env
fi

# Java
if [[ -x /usr/libexec/java_home ]]; then
    export JAVA_HOME=$(/usr/libexec/java_home)
fi

# Python
if [[ -d $HOME/.poetry ]]; then
    source ${HOME}/.poetry/env
fi

# Markdown
export GLAMOUR_STYLE=dark
